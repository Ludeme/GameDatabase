(game "Janes Soppi (Alignment)"  
    (players 2)  
    (equipment { 
        (board 
            (merge 
                {
                (rectangle 3 3) 
                (shift 1.5 0.5 (rotate -90 (wedge 2))) 
                (shift -1.5 0.5 (rotate 90 (wedge 2))) 
                }
            )
            use:Vertex
        )
        (hand Each)
        (piece "Hound" P1 "StepToEmpty")
        (piece "Hare" P2 "StepToEmpty")
        }
    )  
    
    (rules 
        (start {
            (place "Hound1" (handSite P1) count:3)
            (place "Hare2" (handSite P2) count:3)
        })
        
        phases:{
        (phase "Placement"
            (play (move (from (handSite Mover)) (to (sites Empty))) )
            (nextPhase ("HandEmpty" P2) "Movement")
        )
        
        (phase "Movement"
            (play (forEach Piece))
        )
        }
        (end (if (is Line 3) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "This version of the game known as Jänes Soppi  is an alignment game. It was played in Kärla, Estonia.")
        (aliases {"Jänes Soppi (Alignment)"})
        (rules "3x3 intersecting lines, with triangles extending on opposite ends of the large square and the central line extending to the apices. One player plays as three notched sticks, called hares, and the other as three sharp sticks, known as the hounds. Players alternate turns placing a piece on the board. When a player has placed all of their pieces on the board, they move one of their pieces to an empty adjacent spot along the lines. The first player to place their three pieces in a row along the lines of the board wins.")
        (source "Michaelsen  2014: 207-208.")
        (version "1.2.11")
        (classification "board/space/line")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (piece Families {"Abstract" "Themed"})
        (show Edges Diagonal Hidden)
        
    })
    
    (ai
        "Janes Soppi (Alignment)_ai"
    )
)
