(define "DistanceToNearestIceberg"
    (count Steps
        ("OntoEmptyOrIceberg")
        (from)
        (sites Occupied by:Neutral)
    )
)

(define "NearestIcebergs"
    (intersection
        (sites Occupied by:Neutral)
        (sites Distance
            ("OntoEmptyOrIceberg")
            from:(from)
            (exact ("DistanceToNearestIceberg"))
        )
    )
)

(define "OntoEmptyOrIceberg"
    (step 
        (to 
            if:(or
                (= (id Neutral) (who at:(to)))
                (is Empty (to))
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Icebreaker"
    (players 2)
    (equipment
        {
        (board (hex <Board>))
        (piece "Disc" Each
            (move Step
                (to 
                    if:(and
                        (or
                            (= (id Neutral) (who at:(to)))
                            (is Empty (to))
                        )
                        (= 1
                            (-
                                (count Steps
                                    ("OntoEmptyOrIceberg")
                                    (from)
                                    ("NearestIcebergs")
                                )
                                (count Steps
                                    ("OntoEmptyOrIceberg")
                                    (to)
                                    ("NearestIcebergs")
                                )
                            )
                        )
                    )
                    (apply 
                        (if
                            (is Occupied (to))
                            (and
                                (remove (to))
                                (addScore Mover 1)
                            )
                        )
                    )
                )
            )
        )
        (piece "Disc" Neutral)
        }
    )
    (rules
        (start {
            (place "Disc0" (difference (sites Board) (sites Corners)))
            
            (place "Disc1" (sites Around (centrePoint) distance:(- <Board> 1) NNW))
            (place "Disc1" (sites Around (centrePoint) distance:(- <Board> 1) E))
            (place "Disc1" (sites Around (centrePoint) distance:(- <Board> 1) SSW))
            
            (place "Disc2" (sites Around (centrePoint) distance:(- <Board> 1) NNE))
            (place "Disc2" (sites Around (centrePoint) distance:(- <Board> 1) W))
            (place "Disc2" (sites Around (centrePoint) distance:(- <Board> 1) SSE))
            
        })
        (play
            (forEach Piece Mover)
        )
        (end 
            (if 
                (or
                    (<
                        (/ (- (count Sites in:(sites Board)) 6) 2)
                        (score P1)
                    )
                    (<
                        (/ (- (count Sites in:(sites Board)) 6) 2)
                        (score P2)
                    )
                )
                (byScore)
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> }
    {
    (item "8"    <8>  "A size 8 board is currently selected")
    (item "7"    <7>  "A size 7 board is currently selected")
    (item "6"    <6>  "A size 6 board is currently selected")
    (item "5"    <5>  "A size 5 board is currently selected")*
    (item "4"    <4>  "A size 4 board is currently selected")
    }
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Icebreaker is a drawless game for two players competing to break the most ice with their ships. Mark Steere designed Icebreaker in November, 2021.")
        (rules "There are three red ships and three black ships. The remainder
            of the cells contain white icebergs. The two players, Red and Black, take turns
            moving one of their own ships, one move per turn, starting with Red. Players are
            not allowed to pass.
            
            MOVES:
            You must move one of your ships to an adjacent cell which doesn't contain
            another ship. By moving to a cell containing an iceberg, you capture the iceberg,
            and your score increases by 1.
            
            MOVE DIRECTION:
            You must select one of your ships to move, and move it closer to its closest
            iceberg. Distance is measured by the number of cells between ship and iceberg
            along the shortest path of cells that connects them, going around other ships. If
            the ship you've chosen to move has icebergs adjacent to it, you must capture one
            of them.
            
            OBJECT OF THE GAME:
            The goal is to capture the majority of the icebergs. On the default size 5 board, there are 55 icebergs starting out. If you capture 28 of them, you
        win.")
        (id "1926")
        (version "1.3.4")
        (classification "board/war/replacement/eliminate/all")
        (author "Mark Steere")
        (publisher "<a href=\"http://www.marksteeregames.com/\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Mark Steere Games</a>")
        (credit "Michael Amundsen")
        (date "2021")
        }
    )
    
    (graphics {
        (player Colour Neutral (colour White))
        (player Colour P1 (colour Red))
        (player Colour P2 (colour Black))
    })
)
