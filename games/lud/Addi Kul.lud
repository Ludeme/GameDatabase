(game "Addi Kul"
    (players {(player N) (player S)})
    (equipment {
        (board (square 7))
        (piece "Marker" Each
            (or
                (move Step 
                    (directions {Rightward Leftward Forwards})
                    (to if:(is Empty (to)))
                )
                (move Hop
                    (between if:(is Enemy (who at:(between))))
                    (to if:(is Empty (to))
                        (apply (remove (between)))
                    )
                )
            )
        )
    })
    (rules
        (start {
            (place "Marker1" (expand (sites Bottom) steps:2))
            (place "Marker2" (expand (sites Top) steps:2))
        })
        (play
            (forEach Piece)
        )
        (end (if (no Pieces Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "'Addî Kûl is a capturing game played in Tunisia by men. The board is typically made in the sand, and date seeds, stones, or camel droppings are used as pieces.")
        (aliases {"'Addî Kûl"})
        (rules "7x7 board. 21 pieces per player, which begin in the three rows closest to each player. The central row of the board remains empty. Players alternate turns moving one of their pieces forward, diagonally, or horizontally on the board. When a player's piece is next to an opponent's piece with an empty space immediately on the opposite side of it, the player's piece may hop over the opponent's piece to capture it. The player who captures all of the opponent's pieces wins.")
        (source "Ayoub 1991: 23.")
        (id "1787")
        (version "1.3.4")
        (classification "board/war/replacement/eliminate/all")
        (credit "Eric Piette")
        (origin  "This game was played in Tunisia, around 1988.")
        }
    )
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
)
