(define "SomeSite"
    (not
        (all Sites #1 
            if:(not #2)
        )
    )
)

(define "Place"
    (move Add
        (to 
            (sites Around (sites Occupied by:Mover) Empty Orthogonal)
        )
    )
)

(define "Remove"
    (move Select
        (from 
            (sites Occupied by:Enemy)
            if:("SomeSite"
                (sites Group at:(from))
                (and 
                    (= 0 (count Sites in:(sites Around (site) Empty Orthogonal)))
                    (if 
                        (>=
                            11
                            <Size>
                        )
                        (< 0 (count Sites in:(sites Around (site) Own Orthogonal)))
                        True
                    )
                )
            )
        )
        (then (forEach Site (sites Group at:(last From) Orthogonal) (remove (site))))
    )
)

//------------------------------------------------------------------------------

(game "Rampart"
    (players 2)
    (equipment
        {
        (board (square <Size>) use:Vertex)
        (piece "Disc" Each)
        }
    )
    (rules
        (start {
            (place "Disc1" 
                (forEach 
                    (sites Phase 0) 
                    if:(and {
                        (is Even (row of:(site)))
                        (!= 0 (% (site) 4))
                        (= 0 (% (row of:(site)) 4))
                    })
                )
            )
            (place "Disc2" 
                (forEach 
                    (sites Phase 0) 
                    if:(and {
                        (is Even (row of:(site)))
                        (!= 0 (% (site) 4))
                        (= 2 (% (row of:(site)) 4))
                    })
                )
            )
        })
        (play
            (or
                ("Place")
                ("Remove")
            )
        )
        (end 
            (if 
                (no Pieces Next) 
                (result Next Loss)
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Size> args:{ <size> }
    {
    (item "5"    <5>  "A size 5 board is currently selected")
    (item "7"    <7>  "A size 7 board is currently selected")
    (item "9"    <9>  "A size 9 board is currently selected")
    (item "11"    <11>  "A size 11 board is currently selected")
    (item "13"    <13>  "A size 13 board is currently selected")*
    (item "15"    <15>  "A size 15 board is currently selected")
    (item "17"    <17>  "A size 17 board is currently selected")
    (item "19"    <19>  "A size 19 board is currently selected")
    }
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Rampart is an annihilation game in which players grow a set number of groups attempting to capture the opponent's groups.")
        (rules "Play starts with black. On his turn a player may either drop a stone of his color or remove a dead enemy group. Players may only legally drop stones adjacent to at least one of their stones already on the board. For boards sized 11x11 and smaller a group is considered dead if any stone within it meets the following criteria.
            
        a)The stone has no empty points orthogonally adjacent
    b)The stone has at least one enemy stone adjacent
    
For boards larger than 11x11 only the first criterion applies when determining if a group is dead. You may never remove your own dead groups. If a player has no legal move on his turn he must pass. Passing is otherwise not permitted. The player to remove all opposing groups from the board wins.")
(id "1933")
(source "<a href=\"https://boardgamegeek.com/boardgame/133923/rampart\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />BGG</a>")
(version "1.3.4")
(classification "board/war/replacement/eliminate/all")
(author "Corey Clark")
(credit "Michael Amundsen")
(date "2012")
}
)

(graphics {
    (player Colour P1 (colour Black))
    (player Colour P2 (colour White))
    (board Colour InnerEdges (colour 178 151 100))
    (board Colour OuterEdges (colour 178 151 100))
    (board Colour Phase0 (colour 250 210 148))
    (board Colour Phase1 (colour 250 210 148))
    (board Style Board)
})
)
