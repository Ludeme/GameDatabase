(game "Center"  
    (players 2) 
    (equipment 
        { 
        <Board> 
        (piece "Marker" Each)
        }
    )
    
    (rules 
        <Pie>
        (play 
            (move 
                Add
                (to 
                    (sites Empty)
                    if:(<=
                        (count Steps 
                            <Board:direction>
                            (to)
                            (sites Perimeter)
                        )
                        (count Sites 
                            in:(intersection
                                (sites LineOfSight
                                    at:(to)
                                    <Board:direction>
                                )
                                (sites Occupied by:Mover)
                            )
                        )
                    )
                )
            )
        )
        
        (end 
            (if 
                (is Occupied (centrePoint)) 
                (result Next Loss)
            )
        )
    )
)

//-------------------------------------------------------------------------

(option "Board" <Board> args:{ <size> <direction>}
    {
    (item "Square 9x9" <(board (square 9) use:Vertex)> <All> "A square board of size 9 is currently selected")*
    (item "Square 7x7" <(board (square 7) use:Vertex)> <All> "A square board of size 7 is currently selected")
    (item "Hex 4" <(board (hex 4))> <> "A hexagonal board of size 4 is currently selected")
    }
)

(option "Pie rule" <Pie> args:{ <pie> <direction>}
    {
    (item "Off" <> "The pie rule is currenty not in force.")*
    (item "On" <(meta (swap))> "The pie rule is currently in force.")
    }
)

//-------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Center is a strange little game invented by Alek Erickson and Michael Amundsen in February of 2022. The goal is to place a stone on the center of the board, but it's not as easy as it sounds.")
        (rules "Take turns placing stones on a hexhex board of size 4, or a square board of an odd size not larger than 9. The winner is the player who places a stone on the center cell. A placement N steps away from the perimeter must have at least N friendly pieces in sight. On the square board, pieces see in all 8 directions.")
        (id "1930")
        (version "1.3.4")
        (classification "board/race/reach")
        (author "Michael Amundsen")
        (credit "Michael Amundsen")
        (date "02-02-2022")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
        (board Colour InnerEdges (colour 178 151 100))
        (board Colour OuterEdges (colour 178 151 100))
        (board Colour Phase0 (colour 250 210 148))
        (board Colour Phase1 (colour 250 210 148))
        (board Style Board)
        (useFor "Board/Square 7x7" 
            (show Symbol "dot" Vertex {24} 
                edgeColour:(colour 178 151 100) 
                scale:0.18
            )
        )
        (useFor "Board/Square 9x9" 
            (show Symbol "dot" Vertex {20 24 56 60 40} 
                edgeColour:(colour 178 151 100) 
                scale:0.18
            )
        )
    })
)
