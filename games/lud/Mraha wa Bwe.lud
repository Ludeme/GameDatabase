(define "HopCounter" 
    (move
        Hop 
        (from #1) 
        Orthogonal 
        (between 
            if:(is Enemy (who at:(between)))
            (apply (remove (between)))
        ) 
        (to if:(is Empty (to))) 
        #2
    ) 
)

//------------------------------------------------------------------------------

(game "Mraha wa Bwe" 
    (players 2)  
    (equipment { 
        (board (square 5)) 
        (piece "Counter" Each)
        (hand Each)
    })  
    
    (rules 
        (start (place "Counter" "Hand" count:12))
        
        phases:{
        (phase "Placement"
            (play 
                (move 
                    (from (handSite Mover))
                    (to (difference (sites Empty) (sites Centre)))
                    (then (if ("NewTurn") (moveAgain)))
                )
            )
            (nextPhase Mover (is Empty (handSite Mover)) "Play")
        )
        (phase "Play"
            (play 
                (if "SameTurn"
                    (or
                        ("HopCounter" (last To)
                            (then 
                                (if (can Move ("HopCounter" (last To))) 
                                    (moveAgain)
                                ) 
                            ) 
                        )
                        (move Pass)
                    )
                    (or 
                        (forEach Piece "Counter"
                            ("HopCounter" (from) 
                                (then 
                                    (if (can Move ("HopCounter" (last To))) 
                                        (moveAgain) 
                                    ) 
                                ) 
                            ) 
                        )
                        (forEach Piece "Counter" 
                            (move
                                Step 
                                Orthogonal 
                                (to if:(is Empty (to)))
                            )
                        )
                    )
                )
            )
        )
        }
        
        (end (if (no Moves Mover) (result Mover Loss))) 
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Mraha wa Bwe is a capturing game played in Comoros. The game is made on the ground and the pieces are gathered from what is available. Stones are often used, for example.")
        (rules "5x5 board. Twelve pieces per player. Players alternate turns placing two pieces on the board, leaving the central space empty. Once all the pieces have been placed, players alternate turns moving one space orthogonally to an empty space. An opponent's piece may be captured by hopping over it to an empty space immediately on the opposite side of it in an orthogonal direction. Multiple captures are allowed, and a player is allowed to land on a space already landed on during the chain of captures. Captures are not compulsory. A player wins when they capture all of their opponent's pieces or the opponent cannot move.")
        (source "De Voogt 2019: 3-5.")
        (id "1991")
        (version "1.3.4")
        (classification "board/war/leaping/orthogonal")
        (credit "Eric Piette")
        (origin  "This game was played in Comoros, around 2018.")
        }
    )
    
    (graphics {
        (board Style Chess)
        (piece Families {"Defined" "Isometric"})
    })
    
)
