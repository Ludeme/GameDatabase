(game "El-Mthaltha" 
    (players 2)  
    (equipment { 
        ("AlquerqueBoard" 3 3)
        (piece "Marker" Each 
            (move
                Step 
                (to if:(is Empty (to)))
                (then 
                    (custodial 
                        (from (last To))
                        Orthogonal
                        (between
                            (max 1)
                            if:(is Enemy (who at:(between)))
                            (apply (remove (between)))
                        )
                        (to if:(is Friend (who at:(to))))
                    )
                )
            )
        )
    })  
    (rules 
        (start {
            (place "Marker1" (sites Bottom))
            (place "Marker2" (sites Top))
        })
        (play (forEach Piece))
        (end (if (no Pieces Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "El-Mthaltha is a small game with custodial captures played in Tunisia.")
        (aliases {"Thlatha Tishsha" "Thlath Tisha"})
        (rules "3x3 intersecting lines, with diagonals in the square. Three pieces per player, which begin on opposite sides of the square. Players alternate turns moving one of their pieces along the lines of the board. When a player surrounds one of the opponent's pieces with two of their pieces, the opponent's piece is captured. The player who captures all of the opponent's pieces wins.")
        (source "Ayoub 1991: 41.")
        (id "1788")
        (version "1.3.4")
        (classification "board/war/custodial")
        (credit "Eric Piette")
        (origin  "This game was played in Tunisia, around 1988.")
        }
    )
    
)
