(game "Buga Nokhoi"
    (players 2)  
    (equipment { 
        (board 
            (merge {
                (rotate 45 (shift 1.5 4.2 (square 2 diagonals:Solid)))
                (rotate 45 (shift 1.5 -1.2 (square 2 diagonals:Solid)))
                (square 5 diagonals:Alternating)
            })
            use:Vertex
        )
        (piece "Deer" P2 (and ("StepToEmpty") ("HopCapture")))
        (piece "Dog" P1 ("StepToEmpty"))
        (hand P1)
    })  
    
    (rules 
        // “The other player plays with twelve dogs, at least some of which begin in the main part of the board, but some may be introduced later. “
        
        (start {
            (place "Deer2" (sites {"C3" "C7"}))
            (place "Dog1" (handSite P1) count:12) // TODO "at least some of which begin in the main part of the board, but some may be introduced later."
            // WARNING: SOME SHOULD BE PLACED ON THE BOARD AND SOME OTHERS SHOULD BE POTENTIALLY ABLE TO REPLACE THE CAPTURED ONES.... NO IDEA HOW TO MODEL THAT IN A RECONS WAY.
        })
        
        phases:{
        (phase "Placement" P1
            (play
                (move 
                    (from (handSite Mover)) 
                    (to (sites Empty)) 
                    (then (if (is Occupied (handSite P1)) (moveAgain)))
                )  
            ) 
            (nextPhase Mover ("HandEmpty" P1) "Movement")
        )
        
        (phase "Movement" (play (forEach Piece)))
        }
        
        (end {
            (if (no Pieces P1) (result P2 Win))
            (if (no Moves P2) (result P1 Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Buga Nokhoi is a hunt game played in Mongolia. It is thought to either reflect a deer hunt, or may reflect the symbolic nature of deer the deer, which are not captured as one would expect in a hunt.")
        (rules "5x5 intersecting lines, with diagonals in each 3x3 quadrant of the board. On opposite sides, a square with diagonals, with one corner as the midpoint of one of the sides of the main board. One player plays as two deer, which begin on the points where the small squares intersect with the main board. The other player plays with twelve dogs, at least some of which begin in the main part of the board, but some may be introduced later. Pieces move to an adjacent empty spot along the lines of the board. The deer may capture an adjacent dog by hopping over it to an empty space immediately on the opposite side of it along the lines of the board. the goal of the deer is to capture all of the dogs, the goal of the dogs is to block the deer from being able to move.")
        (source "DLP evidence.")
        (id "1004")
        (version "1.3.4")
        (classification "board/hunt/reconstruction")
        (credit "Eric Piette")
        (origin  "This game was played in Mongolia, from around 1988 to 1991.")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
    })
    
)
