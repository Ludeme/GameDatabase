(game "Flower Shop"
    (players 2)
    (equipment {
        (board (hex 7))
        (tile "Hex" Each)
        (piece "Disc" Neutral)
    })
    (rules
        (play
            (do
                (or
                    (move Add
                        (piece (mover))
                        (to 
                            (sites Empty)
                            if:(= 0
                                (count Sites
                                    in:(intersection
                                        (sites Occupied by:All component:"Hex")
                                        (sites Around (to) Orthogonal)
                                    )
                                )
                            )
                        )
                    )
                    (move Add
                        (piece "Disc0")
                        (to (sites Empty))
                    )
                )
                ifAfterwards:(and
                    (all Sites
                        (sites Occupied by:All component:"Hex")
                        if:(> 2
                            (count Sites
                                in:(intersection
                                    (sites Around
                                        (site)
                                        Orthogonal
                                    )
                                    (sites Occupied by:Neutral)
                                    
                                )
                            )
                        )
                    )
                    (all Sites
                        (sites Occupied by:Neutral)
                        if:(not
                            (and
                                (< 0
                                    (count Sites
                                        in:(sites Around
                                            (sites Group at:(site))
                                            Own
                                            Orthogonal
                                        )
                                    )
                                )
                                (< 0
                                    (count Sites
                                        in:(sites Around
                                            (sites Group at:(site))
                                            Enemy
                                            Orthogonal
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
                (then
                    (if
                        (= 1 (% (+ 1 (count Moves)) 2))
                        (moveAgain)
                    )
                )
            )
        )
        (end 
            (if 
                (all Passed) 
                (byScore {
                    (score P1
                        (+
                            (results
                                from:(intersection
                                    (sites Occupied by:P1)
                                    (sites Around (sites Occupied by:Neutral) Orthogonal)
                                )
                                to:1
                                (* 
                                    (to)
                                    (size Group
                                        at:(+
                                            (results
                                                from:(intersection
                                                    (sites Around (from) Orthogonal)
                                                    (sites Occupied by:Neutral)
                                                )
                                                to:1
                                                (* (from) (to))
                                            )
                                        )
                                        Orthogonal
                                        if:(= (id "Disc0") (what at:(to)))
                                    )
                                )
                            )
                        )
                    )
                    (score P2
                        (+
                            (results
                                from:(intersection
                                    (sites Occupied by:P2)
                                    (sites Around (sites Occupied by:Neutral) Orthogonal)
                                )
                                to:1
                                (* 
                                    (to)
                                    (size Group
                                        at:(+
                                            (results
                                                from:(intersection
                                                    (sites Around (from) Orthogonal)
                                                    (sites Occupied by:Neutral)
                                                )
                                                to:1
                                                (* (from) (to))
                                            )
                                        )
                                        Orthogonal
                                        if:(= (id "Disc0") (what at:(to)))
                                    )
                                )
                            )
                        )
                    )
                })
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata
    
    (info
        {
        (description "Flower Shop is a colorful dynamic connection game invented by Mihaw Zapawa.")
        (rules "Flower Shop is played on a hexhex board and uses the 12* placement protocol, that is: barring the first player's first placement, which must be a single piece, players make two placements in a turn. There are two types of pieces: impartial *stalks* (green) and partial *flowers* (pink and yellow). A maximal set of connected stalks is called a *plant*.
            
            Rules
            There are two placement restrictions:
            1. Each flower may be adjacent to no more than one stalk and no other flowers.
            2. Each plant can only be adjacent to at most one colour of flowers.
            
            The game ends when no legal placements can be made. The *worth* of each plant is the number of stalks in it times the number of flowers adjacent to it. The *score* of each player is the total worth of all the plants adjacent to his flowers. Highest score wins. In case of equal scores, no player wins.
            
        (Description taken - but slighlty adapted - from mindsports.nl.)")
        (id "1931")
        (version "1.3.4")
        (classification "board/space/territory")
        (author "Mihaw Zapawa")
        (credit "Michael Amundsen")
        (date "2021")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Pink))
        (player Colour P2 (colour Yellow))
        (piece Colour Neutral "Disc"
            fillColour:(colour Green)
            strokeColour:(colour Green)
        )
        (piece Scale Neutral "Disc" 0.7)
        (region Colour (sites Board) (colour VeryLightBlue))
        (board Colour InnerEdges (colour Turquoise))
        (board Colour OuterEdges (colour Turquoise))
    })
)

