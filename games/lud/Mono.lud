(define "CountAndMarkOddGroupsMover"
    (and
        (set Score Mover 0)
        (forEach Group Orthogonal
            if:(is Mover (who at:(to)))
            (if 
                (is Odd (count Sites in:(sites)))
                (and
                    (addScore Mover -1)
                    (forEach Site 
                        (sites)
                        (set State at:(site) 1)
                    )
                )
                (forEach Site 
                    (sites)
                    (set State at:(site) 0)
                )
            )
        )
    )
)

//--------------------------------------

(game "Mono"
    (players 2)
    (equipment
        {
        (board (hex <Size>))
        (piece "Disc" Each)
        }
    )
    (rules
        (play
            (move Add
                (to (sites Empty))
                (then ("CountAndMarkOddGroupsMover"))
            )
        )
        (end
            (if 
                (is Full) {
                (if
                    (=
                        (count Groups if:(= (id P1) (who at:(to))))
                        (count Groups if:(= (id P2) (who at:(to))))
                    )
                    (byScore)
                )
                (if 
                    True 
                    (byScore {
                        (score P1 (- (count Groups if:(= (id P1) (who at:(to))))))
                        (score P2 (- (count Groups if:(= (id P2) (who at:(to))))))
                    })
                )
            })
        )
    )
)

//--------------------------------------

(option "Board Size" <Size> args:{ <size> }
    {
    (item "3"    <3>  "A size 3 board is currently selected")
    (item "4"    <4>  "A size 4 board is currently selected")
    (item "5"    <5>  "A size 5 board is currently selected")*
    (item "6"    <6>  "A size 6 board is currently selected")
    (item "7"    <7>  "A size 7 board is currently selected")
    (item "8"    <8>  "A size 8 board is currently selected")
    }
)

//--------------------------------------

(metadata
    (info
        {
        (description "Mono is a simple unification game of pure placement invented by Michael Amundsen in May 2022. It is a variant of Elea, by the same inventor.")
        (rules "Players take turns placing a stone of their own color on any empty cell of a hexagonal grid of hexagons (a hexhex board). 
            The game is over when the board is full, and the player with the lower score wins. 
            If both players have the same number of groups, your score is your number of odd-sized groups, otherwise it is simply your number of groups.
            
            The running score only shows the number of odd groups (with a minus sign in front of it, for technical reasons).
            
        Groups of odd size is marked with a red outline.")
        (id "1985")
        (version "1.3.4")
        (classification "board/space/group")
        (author "Michael Amundsen")
        (credit "Michael Amundsen")
        (date "2022")
        }
    )
    (graphics {
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
        (piece Colour state:0 strokeColour:(colour Black))
        (piece Colour state:1 strokeColour:(colour Red))
        (region Colour (sites Board) (colour 250 210 148))
        (board Colour InnerEdges (colour 178 151 100))
        (board Colour OuterEdges (colour 178 151 100))
    })
    (ai
        (bestAgent "MC-GRAVE")
    )
)
