(game "To Kinegi tou Lagou"
    (players 2)  
    (equipment { 
        (board 
            (add
                (remove
                    (remove
                        (merge {
                            (concentric {1 8})
                            (shift 0 -5 (concentric {1 4}))
                            (shift 0 5 (concentric {1 4}))
                            (shift -5 0 (concentric {1 4}))
                            (shift 5 0 (concentric {1 4}))
                            (shift 5 5 (concentric {1 8}))
                            (shift 5 -5 (concentric {1 8}))
                            (shift -5 -5 (concentric {1 8}))
                            (shift -5 5 (concentric {1 8}))
                        })
                        vertices:{20 18 27 11 48 49 50 51 52 41 42 43 44 45 34 35 36 37 30 59 58 57 64 63}
                    )
                    edges:{
                    {0 1} {0 7} {0 5} {0 3} {33 35} {30 29} {27 25} {39 37}
                    }
                )
                edges:{
                {15 8}  {22 6}  {4 12}  {19 2}  {18 36}  {38 20}  {40 14}  {16 26}
                {28 24}  {23 32}  {31 11}  {10 34}  {5 30}  {3 35}  {39 1}  {27 7}              
                }
            )
            use:Vertex
        )
        (piece "Human" Each ("StepToEmpty"))
        (piece "Dog" Each ("StepToEmpty"))
        (piece "Hare" Each ("StepToEmpty"))
        (hand Each size:2)
    })  
    
    (rules 
        (start {
            (place "Hare2" (centrePoint))
            (place "Human1" (handSite P1))
            (place "Dog1" (handSite P1 1) count:2)
        })
        
        <Rule:rule>
    )
)

//------------------------------------------------------------------------------

(option "Rules" <Rule> args:{ <rule> } {
    
    (item "Two Phases" 
        <
        phases:{
        (phase "PlacementP1" 
            (play
                (move 
                    (from (sites Hand P1) if:(is Occupied (from)))
                    (to (sites {1..8}) if:(is Empty (to)))
                    (then (if (not ("HandEmpty" P1)) (moveAgain)))
                )
            )
            (nextPhase ("HandEmpty" P1) "HuntingP2")
        )
        (phase "HuntingP2" 
            (play 
                (forEach Piece
                    (then 
                        (if (not 
                                (can Move 
                                    (step 
                                        (from (where "Hare" P2))
                                        (to if:(is Empty (to)))
                                    )
                                )
                            )
                            (and {
                                (addScore P2 (- (count Moves) 3))
                                (remove (sites Occupied by:All container:"Board"))
                                (add (piece "Hare1") (to (sites Centre)))
                                (add (piece "Human2") (to (handSite P2)))
                                (add (piece "Dog2") (to (handSite P2 1)) count:2)
                            })
                        )
                    )
                )
            )
            (nextPhase (= 1 (count Sites in:(sites Occupied by:All container:"Board"))) "PlacementP2")
        )
        (phase "PlacementP2" 
            (play
                (move 
                    (from (sites Hand P2) if:(is Occupied (from)))
                    (to (intersection (sites Empty) (sites {1..8})) if:(is Empty (to)))
                    (then (if (not ("HandEmpty" P2)) (moveAgain)))
                )
            )
            (nextPhase ("HandEmpty" P2) "HuntingP1")
        )
        (phase "HuntingP1" 
            (play 
                (forEach Piece
                    (then 
                        (if (not 
                                (can Move 
                                    (step 
                                        (from (where "Hare" P1))
                                        (to if:(is Empty (to)))
                                    )
                                )
                            )
                            (addScore P1 (- (count Moves) (+ (score P2) 6)))
                        )
                    )
                )
            )
            (end (if (!= 0 (score P1)) (byScore)))
        )
        }
        >  
        "The game has two rounds."
    )*
    
    (item "One Round" 
        <
        phases:{
        (phase "PlacementP1" 
            (play
                (move 
                    (from (sites Hand P1) if:(is Occupied (from)))
                    (to (sites {1..8}) if:(is Empty (to)))
                    (then (if (not ("HandEmpty" P1)) (moveAgain)))
                )
            )
            (nextPhase ("HandEmpty" P1) "HuntingP2")
        )
        (phase "HuntingP2" 
            (play (forEach Piece))
            (end {
                (if (no Moves P2) (result P1 Win))
                (if 
                    (or
                        (>= (count Moves) (- (value MoveLimit) 10)) 
                        (>= (count Turns) (- (value TurnLimit) 5)) 
                    )
                    (result P2 Win)
                )
            }) 
        )
        }
        >  
        "The game has only one round."
    )
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/To Kynegi tou Lagou (Observed)" {
        "Rules/Two Phases"
    })
    
    (ruleset "Ruleset/Rabbit Wins One Round (Suggested)" {
        "Rules/One Round"
    })
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "To Kynégi tou Lagoú (\"Hare Hunt\") is a blockade game played by children in Greece. Played on a square board, the game is played by three people; one moving the hare, one moving the hunter, and the other moving the two hounds. ")
        (aliases {"To Kynégi tou Lagoú" "The Game of Chasing the Hare"})
        (useFor "Ruleset/To Kynegi tou Lagou (Observed)" (rules "A square, with lines bisecting the sides drawn to the center. A circle is drawn around the central point. arcs are drawn inside the square around the midpoints of the sides and the corners. Diagonals are drawn from the corner arcs to the central circle. One player plays as the hare, one player plays as the hunter and two hounds. The hare begins on the central spot, and the hunter and hounds are placed on three of the points on the central circle. Pieces move to an empty adjacent spot along the lines of the board. The hare moves first. When the hunter and hounds prevent the hare from being able to move, the game ends and the players switch roles."))
        (useFor "Ruleset/To Kynegi tou Lagou (Observed)" (source "Argyriadis 1997: 93-95."))
        (useFor "Ruleset/To Kynegi tou Lagou (Observed)" (id "1166"))
        (useFor "Ruleset/Rabbit Wins One Round (Suggested)" (rules "Only played in one round. If the hunter and dogs cannot block the rabbit before the game times out, the raabbit wins."))
        (useFor "Ruleset/Rabbit Wins One Round (Suggested)" (id "1960"))
        (source "Michaelsen 2014: 211-212.")
        (version "1.3.4")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin  "This game was played in Greece, from around 1906 to 1983.")
        }
    )
    
)
