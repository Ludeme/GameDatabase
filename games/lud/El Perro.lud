(define "Hop" 
    (move Hop 
        (from #1) 
        Adjacent
        (between 
            if:(is Enemy (who at:(between))) 
            (apply (remove (between)))
        ) 
        (to if:(is Empty (to))) 
        #2
    ) 
)

//------------------------------------------------------------------------------

(game "El Perro"
    (players 2)  
    (equipment { 
        ("AlquerqueBoard" 5 5)
        (piece "Goat" P2
            (move Step
                (if (= (value Player Mover) 1)
                    (directions {S SE SW E W})
                    (directions {N NE NW E W})
                )
                (to if:(is Empty (to)))
            )
        )
        (piece "Dog" P1 
            (or 
                ("Hop" 
                    (from)
                    (then (if (can Move ("Hop" (last To)) ) (moveAgain) )) 
                ) 
                "StepToEmpty"
            )
        )
    })  
    
    (rules 
        (start {
            (place "Goat2" (union (intersection (union (sites Left) (sites Right)) (sites Row 2)) (expand (sites Bottom))))
            (place "Dog1" (sites Centre))
        })
        (play
            (if ("SameTurn")
                (or
                    ("Hop" 
                        (last To)
                        (then (if (can Move ("Hop" (last To)) ) (moveAgain) )) 
                    ) 
                    (move Pass)
                )
                (forEach Piece)
                (then
                    (if (not 
                            (can Move
                                (forEach Piece 
                                    (step
                                        (if (= (value Player P2) 1)
                                            (directions {S SE SW})
                                            (directions {N NE NW})
                                        )	
                                        (to if:(is Empty (to)))
                                    )	
                                    P2
                                )
                            )
                        )
                        (set Value P2 (if (= (value Player P2) 1) 0 1))
                    )
                )
            )
        )
        (end {
            (if (no Moves P1) (result P2 Win))
            (if (no Pieces P2) (result P1 Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "El Perro is a hunt game played in the Canary Islands. The board is typically scratched n the ground or on a stone, and the pieces are stones, seeds or shells.")
        (rules "5x5 intersecting lines, with diagonals drawn in the quadrants. One player plays as one dog,  which is a large stone, and the other as twelve goats, which are smaller stones. The goats begin on the two rows closest to the player to which they belong, and on the right hand spots in the central row. The dog begins in the central space. 
            
            The dog moves first. The dog may move in any direction along the lines of the board. It may capture a goat by hopping over it to an empty adjacent spot on the opposite side of the goat, according to the lines of the board. Multiple captures are allowed. The goats move one space forward orthogonally or diagonally, or sideways, along the lines of the board. 
            
            When all of the goats are unable to move forward anymore and all are are in spaces being equivalent to the starting position on the opposite side of the board, they may then commence moving in the opposite direction toward their original starting position, but not backwards with respect to this direction. 
            
        The dog wins by capturing all the goats; the goats win by blocking the dog from being able to move.")
        (source "Espinel Cejas and Gonzalez Garc√≠a 1994: 139-141.")
        (id "1942")
        (version "1.3.4")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin  "This game was played in Canary Islands, from around 1958 to 1993.")
        }
    )
    
    (graphics {
        (piece Families {"Abstract" "Themed"})
    })
    
)
