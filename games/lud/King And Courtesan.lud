(game "King And Courtesan"
    (players {(player N) (player S)})
    (equipment
        {
        (board (rotate 45 (square <Size>)))
        (piece "Disc" Each
            (or {
                (move Step
                    Forwards
                    (to 
                        if:(is Empty (to)) 
                    )
                    stack:True
                )
                (move Step
                    (to 
                        if:(is Enemy (who at:(to))) 
                        (apply 
                            (and
                                (if
                                    (= 2 (size Stack at:(to)))
                                    (set Var "NextLoss" 1)
                                )
                                (remove (to) count:(size Stack at:(to)))
                            )
                        )
                    )
                    stack:True
                )
                (move Step
                    (from if:(= 2 (size Stack at:(from))))
                    Forwards
                    (to 
                        if:(is Mover (who at:(to)))
                    )
                )
            })
        )
        }
    )
    (rules
        (start {
            (place "Disc1" (expand (sites Bottom) steps:(- <Size> 2) Orthogonal))
            (place "Disc2" (expand (sites Top) steps:(- <Size> 2) Orthogonal))
            (place Stack "Disc1" (sites Bottom))
            (place Stack "Disc2" (sites Top))
        })
        (play
            (forEach Piece top:True)
        )
        (end {
            (if 
                (= 1 (var "NextLoss")) 
                (result Next Loss)
            )
            (if 
                (or
                    (and
                        (= (id P1) (who at:(- (* <Size> <Size>) 1)))
                        (= 2 (size Stack at:(- (* <Size> <Size>) 1)))
                    )
                    (and
                        (= (id P2) (who at:0))
                        (= 2 (size Stack at:0))
                    )
                )
                (result Mover Win)
            )
        })
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Size> args:{ <size> }
    {
    (item "6x6"    <6>  "A size 6 board is currently selected")*
    (item "7x7"    <7>  "A size 7 board is currently selected")
    (item "8x8"    <8>  "A size 8 board is currently selected")
    }
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "King and Courtesan is a drawless two player army game where each player seeks to get their king into the enemy home square, or kill the enemy king. King and Courtesan was invented by Mark Steere in May 2022.")
        (rules "GOAL
            Get your king into the enemy home square, or kill the enemy king.
            
            MOVES
            Players make one move per turn, starting with Red. There are three possible
            types of moves, explained below. Players will always have a move available,
            and must make one. Passing is not allowed.
            
            NON-CAPTURING MOVES
            Kings and courtesans can move to an adjacent, unoccupied square in any of
            the three forward directions.
            
            CAPTURING MOVES
            Kings and courtesans can move to an adjacent, enemy occupied square in any
            of eight directions, capturing the enemy king or courtesan by replacement.
            
            EXCHANGE MOVE
            You can transfer the top checker of your king onto an adjacent, friendly
            courtesan in any of the three forward directions, thus exchanging king and
            courtesan.
            
            In the Ludii implementation, the king is controlled like this: 
            To make an exchange move you must drag or click the top piece.
        To make a step/capture move, you must drag or click the bottom piece.")
        (id "1993")
        (source "<a href=\"http://www.marksteeregames.com/King_and_Courtesan_rules.pdf\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Mark Steere Games</a>")
        (version "1.3.4")
        (classification "board/war/replacement/eliminate/target")
        (author "Mark Steere")
        (publisher "<a href=\"http://www.marksteeregames.com/\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Mark Steere Games</a>")
        (credit "Michael Amundsen")
        (date "2022")
        }
    )
    (graphics {
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
        (piece Colour strokeColour:(colour DarkGrey))
        (board Style Chess)
    })
)
