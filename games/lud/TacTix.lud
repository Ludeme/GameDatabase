(game "TacTix"  
    (players 2)  
    (equipment { 
        (board (square 4))
        (piece "Ball" Shared) 
    })  
    
    (rules 
        (start (place "Ball" (sites Board)))
        
        (play 
            (if (is Prev Mover)
                (or
                    (if (= 1 (count MovesThisTurn))
                        (move Select
                            (from (last To))
                            (to (intersection (sites Around (last To) Orthogonal) (sites Occupied by:Shared)))
                            (then
                                (and
                                    (remove (last To))
                                    (if 
                                        (and
                                            (is Occupied (ahead (last To) (directions Cell from:(last From) to:(last To))) ) 
                                            (!= (last To) (ahead (last To) (directions Cell from:(last From) to:(last To))))
                                        )
                                        (moveAgain)
                                    )
                                )
                            )
                        )
                        (move Select
                            (from (last To))
                            (to (ahead (last To) (directions Cell from:(last From) to:(last To))))
                            (then 
                                (and
                                    (remove (last To))
                                    (if  (and
                                            (is Occupied (ahead (last To) (directions Cell from:(last From) to:(last To))) ) 
                                            (!= (last To) (ahead (last To) (directions Cell from:(last From) to:(last To))))
                                        )
                                        (moveAgain)
                                    )
                                )
                            )
                        )
                    )
                    (move Pass)
                )
                (move Remove
                    (sites Board)
                    (then (if (not (all Sites (sites Around (last To) Orthogonal) if:(is Empty (site)))) (moveAgain)))
                )
            )
        )
        
        (end (if (no Moves Next) (result Next Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Impartial game played with the 4x4 board full of one kind of piece.
            
            A number of \"folk art\" versions of the game with a wooden board and marbles were found on the web. Computer program versions are also available.
            
        The 4x4 game has been completely solved and it is a second player win.")
        (aliases {"Bulo"})
        (rules "A player may remove one or more adjacent pieces from a row or column. The loser is the person who takes the last piece off the board.")
        (id "1824")
        (source "<a href=\"https://boardgamegeek.com/boardgame/25895/tactix\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />BGG</a>
            
        <a href=\"http://www.di.fc.ul.pt/~jpn/gv/bulo.htm\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />www.di.fc.ul.pt</a>")
        (version "1.3.4")
        (classification "board/space/blocking")
        (author "Piet Hein")
        (credit "Eric Piette")
        (date "1949")
        }
    )
    
    (graphics {
        (no Sunken)
        (board Colour Phase0 (colour DarkBrown))
        (board Colour OuterEdges (colour VeryDarkBrown))
        (board Colour InnerEdges (colour VeryDarkBrown))
        (player Colour Shared (colour VeryDarkBrown))
        (show Symbol "disc" (sites Board) fillColour:(colour Brown) edgeColour:(colour Brown) scale:0.9)
    })
    
)
