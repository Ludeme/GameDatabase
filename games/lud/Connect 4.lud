(define "ColumnSize" 6 )

(define "EmptyTop" (intersection (sites Top) (sites Empty)))

(define "LastColumn" (sites Column (column of:(last To))))

(define "Distance" (count Sites in:(intersection (sites Empty) "LastColumn")))

(define "Drop" (slide (from (last To)) S (between (exact "Distance"))))

//------------------------------------------------------------------------------

(game "Connect 4"  
    (players 2)  
    (equipment { 
        <Model:board>
        (piece "Disc" Each)
    }) 
    
    (rules
        <Model:rules>
    )
)

//------------------------------------------------------------------------------

(option "Modelisation" <Model> args:{ <board> <rules> }
    {
    (item "1D"      
        <(board (rect 1 7))>
        <    
        (play (add (mover) to:(forEach (sites Board) (< (size Stack at:(to)) "ColumnSize")) stack:true))
        (end (if (is Line 4) (result Mover Win)))
        >
        ""
    )**   
    (item "2D"         
        <(board (rect 6 7))>
        <
        
        (play (add (mover) to:("EmptyTop") (then "Drop")))
        (end (if (is Line 4 through:(last To afterConsequence:true)) (result Mover Win)))
        >
        ""
    )   
})

(metadata
    
    (info
        {
        (description "Connect Four is a two-player connection game that was designed by Howard Wexler and Ned Strongin, and was first sold under this name by Milton Bradley in 1974.")
        (aliases {"Four Up" "Plot Four" "Find Four" "Four in a Row" "Four in a Line" "Drop Four" "Gravitrips"})
        (rules "Connect 4 is played on a vertically placed grid of 7x6, where colored disks are dropped from the top of the grid. Players alternate dropping discs, which fall to the bottom of the column in which they are dropped. The first player to create a row of four disks in their color wins.")
        (source "https://en.wikipedia.org/wiki/Connect_Four")
        (version "0.9.3")
        (classification "board/space/line")
        (author "Howard Wexler, Ned Strongin")
        (publisher "Milton Bradley / Hasbro")
        (credit "Eric Piette")
        (date "1974")
        }
    )
    
    (graphics {
        (playerColour P1 (colour Yellow))
        (playerColour P2 (colour Red))
        (useFor {"Modelisation/1D"} (boardStyle Connect4Style))
        (useFor {"Modelisation/2D"} (boardStyle PyramidalStyle))
        (boardStyle Connect4Style)
        (noAnimation true)
        (selectClosestPiece true)
    })
    
    (ai 
        "Connect 4_ai"
    )
    
)
